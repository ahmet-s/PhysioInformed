VAR avoidJar_Phy = 1
VAR sufInfo_Phy = 1
VAR applyICE_Phy = 2

VAR pointTakenPhy = false

////////////////***********Physiotherapy
==D_Phy_T_Exp==
~keypointsCompleted = false
~DocTextView(true)
~keypoints -= (PIAT, PEDT, RTfT)
{
- !pointTakenPhy:
~TotalOSCEPoints(avoidJar_Phy, sufInfo_Phy, applyICE_Phy)
~pointTakenPhy = true
}
~speaker = Doctor
{
- treatmentOrder == 0:
Let's start with <>
- treatmentOrder == 1:
Another option is <>
- treatmentOrder == 2:
Final option is <>
}
physiotherapy. You stated in your form that you've taken this treatment when you injured your back.
{
- choiceStatus == 0:
<> ...
->D_Phy_C_Exp
- choiceStatus == 1:
<>. So, I guess you are familiar how the process is. Let me continue with other parts that can be stated.
->P_Phy_C_Exp
- choiceStatus == 2:
<> However briefly, it helps to relieve pain with certain techniques like heat therapy and promotes better movement. Some activities and exercises will also be advised. 
->P_Phy_C_Exp
}

==D_Phy_C_Exp==
* [Continue to next keypoint or treatment]
~choiceStatus = 1
->D_Phy_T_Exp
* [Explain more]
~ finalSufInfo++
~choiceStatus = 2
->D_Phy_T_Exp

==P_Phy_C_Exp==
~speaker = Patient
{
- choiceStatus == 1:
Yes I did. So, I guess, it's the same independent of where the problem is.
- choiceStatus == 2:
Yes, I did. It helped me as you said but of course, my back isn't the same as before.
} 
~choiceStatus = 0
~StartKeypoints()
->DONE


/////******PosImpAfterTreat

==D_Phy_T_PIAT==
~speaker = Doctor
- Good thing is physiotherapy is very effective in terms of pain in your situation. In terms of functionality, the effects are more moderate. 
{
- choiceStatus == 0:
<> ...
->D_Phy_C_PIAT
- choiceStatus == 1:
<> I mean, while your tendinosis and inflamation in bursa won't hurt as much before, some movements such as lying on your shoulder may still put some stress on your rotator cuff.
->P_Phy_C_PIAT
- choiceStatus == 2:
<> I mean, while your shoulder won't hurt as much before, some movements such as lying on your it may still put some stress on your shoulder.
->P_Phy_C_PIAT
}

==D_Phy_C_PIAT==
* [Explain with referencing rotator cuff and bursa]
~choiceStatus = 1
->D_Phy_T_PIAT
* [Explain in general]
~ finalAvoidJar++
~choiceStatus = 2
->D_Phy_T_PIAT

==P_Phy_C_PIAT==
~speaker = Patient
{
- choiceStatus == 1:
Hmm,  so as for as I could understand from this, less pain but possiblely limited movements. As long as, it doesn't limits my work, that's okay.
- choiceStatus == 2:
So, like my back, I'll have to keep an eye on my shoulder, too. I can do that in daily life but I need to be good enough to drive.
} 
~choiceStatus = 0
~keypoints += (PIAT)
{
-LIST_COUNT(keypoints) == 3:
~treatmentOrder++
~DocTextView(false)
->KeypointsCompleted
-else:
~ActivateKeypoints()
->DONE
}


/////******PosEffDuringTreat

==D_Phy_T_PEDT==
~speaker = Doctor
- With this option, getting a side effect is highly inlikely but physiotherapy can cause a moderate amount of discomfort while having it.
{
- choiceStatus == 0:
<> ...
->D_Phy_C_PEDT
- choiceStatus == 1:
<> However, it wouldn't be that way for the whole process, it would be good to rest on therapy days a precaution.
->P_Phy_C_PEDT
- choiceStatus == 2:
<> However, I don't think it would create a difficulty while driving at work but would be good to rest on therapy days as a precaution.
->P_Phy_C_PEDT
}

==D_Phy_C_PEDT==
* [Explain in general]
~choiceStatus = 1
->D_Phy_T_PEDT
* [Explain with considering patient's work]
~ finalApplyICE++
~choiceStatus = 2
->D_Phy_T_PEDT

==P_Phy_C_PEDT==
~speaker = Patient
{
- choiceStatus == 1:
I understand. I can do that as long as it won't carry it's effects on my work days and I can drive.
- choiceStatus == 2:
I see. I can do that.
}
~choiceStatus = 0
~keypoints += (PEDT)
{
-LIST_COUNT(keypoints) == 3:
~treatmentOrder++
~DocTextView(false)
->KeypointsCompleted
-else:
~ActivateKeypoints()
->DONE
}



/////******ReqTimeforTreat

==D_Phy_T_RTfT==
~speaker = Doctor
- To get this treatment, you may need to wait upto 1 and a half months. The therapy process takes about 3 months as once or twice a week and your full recovery could take 4 months.
{
- choiceStatus == 0:
<> ...
->D_Phy_C_RTfT
- choiceStatus == 1:
<> It's important to stay in the schedule though, for the treatment to be effective and to get good results.
->P_Phy_C_RTfT
- choiceStatus == 2:
<> Do you go out of town for a long time as a lorry driver? If so, it may be hard for you to stay in that schedule to consider this option.
->P_Phy_C_RTfT
}

==D_Phy_C_RTfT==
* [Explain in general]
~choiceStatus = 1
->D_Phy_T_RTfT
* [Explain with considering patient's work]
~ finalApplyICE++
~choiceStatus = 2
->D_Phy_T_RTfT

==P_Phy_C_RTfT==
~speaker = Patient
{
- choiceStatus == 1:
I see. As a lorry driver, this might be hard for me to continue right now. At best, I think once a week I could make a visit.
- choiceStatus == 2:
Yes, indeed. Even though, I don't go for a long time, it may be hard to make the visits twice a week.
}
~choiceStatus = 0
~keypoints += (RTfT)
{
-LIST_COUNT(keypoints) == 3:
~treatmentOrder++
~DocTextView(false)
->KeypointsCompleted
-else:
~ActivateKeypoints()
->DONE
}